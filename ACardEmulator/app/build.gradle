apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.vsmartcard.acardemulator"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 3
        versionName "3.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/external/jcardsim/src/main/java'
        main.java.exclude('**/APDUScriptTool.java')
        main.java.exclude('**/JavaxSmartCardInterface.java')
        main.java.exclude('**/CardTerminalSimulator.java')
        main.java.exclude('**/JCSCard*.java')
        main.java.exclude('**/JCSTerminal.java')
        main.java.exclude('**/JCSFactory.java')
        main.java.exclude('**/CardSimulator.java')
        main.java.exclude('**/CAD.java')
        main.java.exclude('**/CardTerminalSimulator.java')
        main.java.srcDirs += 'src/main/external/GidsApplet/src'
        main.java.exclude('**/gidsAppletTests/*.java')
        main.java.srcDirs += 'src/main/external/IsoApplet/src'
        main.java.srcDirs += 'src/main/external/PivApplet/src'
        main.java.srcDirs += 'src/main/external/android-scio/src/main'
        main.java.srcDirs += 'src/main/external/ykneo-openpgp/applet/src'
        main.java.srcDirs += 'src/main/external/ykneo-oath/applet/src'
    }
}

android.applicationVariants.all { variant ->
    def appName
    //Check if an applicationName property is supplied; if not use the name of the parent project.
    if (project.hasProperty("applicationName")) {
        appName = applicationName
    } else {
        appName = parent.name
    }

    variant.outputs.each { output ->
        def newApkName
        //If there's no ZipAlign task it means that our artifact will be unaligned and we need to mark it as such.
        if (output.zipAlign) {
            newApkName = "${appName}-${output.baseName}-${variant.versionName}.apk"
        } else {
            newApkName = "${appName}-${output.baseName}-${variant.versionName}-unaligned.apk"
        }
        output.outputFile = new File(output.outputFile.parent, newApkName)
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.android.support:design:23.2.1'
    compile 'com.journeyapps:zxing-android-embedded:3.2.0@aar'
    compile 'com.google.zxing:core:3.2.1'
    compile 'com.madgag.spongycastle:core:1.51.0.0'
    compile files('libs/sdk-v1.0.0.jar')
}
